/*

DSL for the gzip program

TODO: verbose is a counter, needs to default to 0

Help goes from the official gzip man pages.

Interesting issues:

* -# parameter is in fact ten other parameters: -1, -2, ...; -1, -2, ..., -9, --fast, --best modifies the same option, which has to be virtual
* verbose is a counter, increased by -v and reset by -q
* --test and --list forces decompression and stdout
* "-" is an argument, meaning standard input

*/

NAME::
 Gzip


ARGS::

	ascii:{"-a","--ascii"}
		: "Ascii text mode: convert end-of-lines using local conventions. This option is supported only on some non-Unix systems. For MSDOS, CR LF is converted to LF when compressing, and LF is converted to CR LF when decompressing."
	stdout:{"-c","--stdout","--to-stdout"}
		: "Write output on standard output; keep original files unchanged. If there are several input files, the output consists of a sequence of independently compressed members. To obtain better compression, concatenate all input files before compressing them."
	decompress:{"-d","--decompress","--uncompress"}
		: "Decompress."
	force:{"-f","--force"}
		: "Force compression or decompression even if the file has multiple links or the corresponding file already exists, or if the compressed data is read from or written to a terminal. If the input data is not in a format recognized by gzip, and if the option --stdout is also given, copy the input data without change to the standard output: let zcat behave as cat. If -f is not given, and when not running in the background, gzip prompts to verify whether an existing file should be overwritten."
	help:{"-h","--help"}
		: "Display a help screen and quit."
	list:{"-l","--list"}
		: "For each compressed file, list the following fields: compressed size, uncompressed size, ratio."
	license:{"-L","--license"}
		: "Display the gzip license and quit."
	noname:{"-n","--no-name"}
		: "When compressing, do not save the original file name and time stamp by default."
	name:{"-N","--name"}
		: "When compressing, always save the original file name and time stamp; this is the default."
	quiet:{"-q","--quiet"}
		: "Suppress all warnings."
	recursive:{"-r","--recursive"}
		: "Travel the directory structure recursively."
	rsyncable:{"--rsyncable"}
		: "While compressing, synchronize the output occasionally based on the input."
	test:{"-t","--test"}
		: "Test. Check the compressed file integrity."
	verbose:{"-v","--verbose"}:{counted-boolean}:[countmax="3",warnonexceedingmax]
		: "Verbose. Display the name and percentage reduction for each file compressed or decompressed."
	version:{"-V","--version"}
		: "Version. Display the version number and compilation options then quit."
	compressionlevel:{"-"}:{int}:[suffixregexp="([1-9])\\00",maxvalue="9",minvalue="1",default="6"]
		: "Regulate the speed of compression using the specified digit. The default compression level is -6."
	compressionlevelbest:{"--best"}
		: "Regulate the speed of compression: the slowest compression method (best compression)."
	compressionlevelfast:{"--fast"}
		: "Regulate the speed of compression: the fastest compression method (less compression)."
	suffix:{"-S","--suffix"}:{string}
		: "Use suffix xxx instead of .gz."

	files:{}:{file-list}:[between="", allowMultiple="false"]
	dashfiles:{}:{file-list}:[between="", allowMultiple="false", allowDash="true"]
	dashdash:{"--"}

FORMAT::

	(option|files)* (dashdash dashfiles*)?;

WHERE::

	option:
		ascii OR stdout OR decompress OR force OR help OR
		list OR license OR noname OR name OR quiet OR recursive
		OR rsyncable OR test OR verbose OR version OR
		compressionlevel OR compressionlevelbest OR
		compressionlevelfast OR suffix;

FLY::
	// Compression level aliases
	compressionlevelbest -> compressionlevel:={9}, compressionlevelfast:={false};
	compressionlevelfast -> compressionlevel:={1}, compressionlevelbest:={false};
	
	// List forces decompress and stdout
	list -> decompress:={true};
	list -> stdout:={true};

	// Test forces decompress and stdout
	test -> decompress:={true};
	test -> stdout:={true};

	// Quiet and verbose are reversed
	quiet -> verbose:={0};
	verbose -> quiet:={false};

OVERRIDES::
	// Default compression level is 6
	{$(compressionlevel?)} -> compressionlevel:={6};

VALIDITY::

