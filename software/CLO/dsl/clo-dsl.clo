
NAME::
 CLODSL

PACKAGE::
 ie.ucd.clops.dsl.generatedinterface

ARGS::

input:{"-i","--input"}:{file}:[mustExist,canbedir="false"]
  :"Input file."
output:{"-o","--output"}:{file}:[mustExist,mustbedir]
  :"Output directory for generated files."
//Strip quotes so we can write -p="" or -p "" for default package.
output_package:{"-p","--package"}:{string}:[stripquotesifpresent]
  :"Output package. Left empty uses the default package."
gen_test:{"-t","--test"}
  :"Generate a Main class with a main method for rapid testing/debugging."

//option_factory should probably be a regular expression (for qualified java class names).
option_factory:{"-of","--option-factory"}:{string}
  :"Use this option factory instead of the default. Must be a fully qualified class name."

gen_docs:{"-d","--docs"}:{file}:[canBeDir="false"]
  :"Generate documentation and write it to the given output file."
gen_docs_builtin:{"-b","--built-in"}:{string-enum}:[choices="html,txt"]
  :"Use a built-in template for documentation generation."
gen_docs_custom:{"-c","--custom"}:{file}:[mustExist,canBeDir="false"]
  :"Use a custom template for documentation generation."

FORMAT::

//Currently enforcing presence of these options through this format.
//When we have validity up and running this will not be necessary
  all_args*
;

WHERE::

all_args: required_args OR optional_args;

required_args:  input OR output;

optional_args:    output_package 
               OR option_factory 
               OR gen_test 
               OR gen_docs
               OR gen_docs_builtin
               OR gen_docs_custom
               ;

FLY::

OVERRIDES::

VALIDITY::

//input != ? && output != ?
